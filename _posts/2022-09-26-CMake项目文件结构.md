---
layout:     post
title:      "CMake项目文件结构"
date:       2022-09-27 12:00:00
author:     "Gnoy-Nus"
header-img: "img/post-bg-default.jpg"
catalog: true
tags:
    - cmake
---



# 引言

CMake是一个C++项目的构建工具，通过CMakeLists.txt文件定义项目的编译和构建的方式。

虽然CMake很难用，但大多数的C++项目都是使用CMake构建的，因此对于想要真正入门C++的学习者来说，学习CMake的使用非常有必要，通过学习CMake，我们也能更深入的理解github上一众C++项目的整体逻辑。

在这篇文章中，我通过一个简单但比较全面、规范的CMake项目模板来说明CMake项目该如何规划以及各个文件夹的作用。

tips：文末附有项目模板的**下载链接**

# 项目文件结构

![image-20220925101327762](https://gnoy-nus.github.io/img/in-post/cmake_structure/imgs/image-20220925101327762.png)

首先给出项目的整体结构，可以看到主要包括

- .gitignore文件：在git add时将我们指定的一些文件自动排除在外，不提交到git当中
- README.md：项目的简单说明文档
- LICENSE.md：存放LICENSE信息
- scripts文件夹：存放项目的脚本文件，例如：自定义run.bat文件，包含cmake的配置和构建指令，这样每次构建就可以一键完成而不用手动输入命令。
- include、src、apps：存放项目核心代码，下面具体说明
- cmake文件夹：存放.cmake文件，下面具体说明
- tests文件夹：存放测试代码，下面具体说明
- extern文件夹：存放外部库代码，下面具体说明
- docs文件夹：利用doxygen生成文档文件，下面具体说明

项目的文件夹名字并不绝对，在每个项目有每个项目自己的管理代码的方式，不过基本上类似，比如在我自己使用的模板当中，extern文件夹名字是3rdparty，而有些项目是库项目所以不需要apps文件夹。

![image-20220925161735203](https://gnoy-nus.github.io/img/in-post/cmake_structure/imgs/image-20220925161735203.png)

# include、src、apps

`include文件夹`存放项目中的我们自己编写的头文件（.h）。

`src文件夹`存放项目中头文件对应的源文件（.cpp），但不包含main函数所在的cpp文件。

在`src文件夹`中我们定义一个CMakeLists.txt，把`include文件夹`中的头文件和`src文件夹`中的源文件组成一个库，提供给main函数所在的cpp文件使用。

```cmake
# 获取include文件夹中的头文件
file(GLOB HEADER_LIST CONFIGURE_DEPENDS "${PROJECT_SOURCE_DIR}/include/modern/*.h")

# 创建我们自己的库modern_library：lib.cpp+${HEADER_LIST}组成
add_library(modern_library lib.cpp ${HEADER_LIST})

# 将库包含我们的include文件夹
target_include_directories(modern_library PUBLIC ../include)

# 链接第三方库
target_link_libraries(modern_library PRIVATE Boost::boost)
```

`apps文件夹`存放项目中main函数所在的cpp文件，单独建立`apps文件夹`的可以明确项目中需要生成的target（一般来说这些target对应生成exe）

```cmake
# 设置需要生成的target名字
set(TARGET_NAME MainProgram)

# 获取主程序的cpp文件
file(GLOB MAIN_SOURCES CONFIGURE_DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/*.cpp)

# 生成target
add_executable(${TARGET_NAME} ${MAIN_SOURCES})

# 链接我们自己的库，或者第三方库
target_link_libraries(${TARGET_NAME} PRIVATE modern_library )
```

如果不使用第三方库，那么通过include、src、apps三个文件夹就可以很清晰地管理我们的c++项目，并用cmake构建。



# cmake

cmake文件夹主要包含.cmake文件，用途是找到第三方库(FindXXX.cmake)，或者作为编写CMakeLists.txt的辅助文件用于定义一些宏或者函数

FindXXX.cmake详情查看<a href='#find_package'>find_package函数</a>。

创建了cmake文件夹后，并且包含FindXXX.cmake文件的话，一般都要在添加cmake目录到`CMAKE_MODULE_PATH`

```cmake
set(CMAKE_MODULE_PATH "${PROJECT_SOURCE_DIR}/cmake" ${CMAKE_MODULE_PATH})
```

在cmake文件夹中定义的辅助cmake文件在CMakeLists.txt中通过`include(xxx.cmake)`引入

```cmake
 include(cmake/add_FetchContent_MakeAvailable.cmake)
```



# tests

test文件夹就是放测试用的相关文件，包括CMakeLists.txt生成用于测试的target

常用的test库有GoogleTest、Catch

test模块的使用可以下载文末的第一个下载链接查看，我自己不怎么熟，就不细说了。

官方文档：[Step 4: Installing and Testing — CMake 3.24.2 Documentation](https://cmake.org/cmake/help/latest/guide/tutorial/Installing and Testing.html#testing-support)

# extern

extern文件夹用于存放第三方库代码，有些项目中叫3rdparty或者dependencies。

引入第三方库的方式不唯一，对于`find_package`的方式，第三方库的代码并不在项目中；如果使用`FetchContent`模块，第三方库在build目录下的`_deps`目录；如果是`add_subdirectory`的方式引入的，可以将第三方库的代码放在这个文件夹里。

更进一步的，如果是使用git管理项目的话可以通过如下方式将第三方库添加为子模块，效果如下图所示，这样可以方便地更新第三方库。

```bash
git submodule add --branch=release-1.8.0 ../../google/googletest.git extern/googletest
```

![image-20220927193426357](https://gnoy-nus.github.io/img/in-post/cmake_structure/imgs/image-20220927193426357.png)

# docs

docs文件夹存放的是文档，可以使用doxygen自动生成，也可以是手写的文档。

使用doxygen需要安装，对docs进行build后会生成html页面，可以直接查看项目头文件

```cmake
#CMakeLists.txt中用法示例，详情可下载模板查看
set(DOXYGEN_EXTRACT_ALL YES)
set(DOXYGEN_BUILTIN_STL_SUPPORT YES)

doxygen_add_docs(docs modern/lib.h "${CMAKE_CURRENT_SOURCE_DIR}/mainpage.md"
                 WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}/include")
```

![image-20220927204824930](https://gnoy-nus.github.io/img/in-post/cmake_structure/imgs/image-20220927204824930.png)

![image-20220927205705463](https://gnoy-nus.github.io/img/in-post/cmake_structure/imgs/image-20220927205705463.png)

# 引入第三方库

> 在 C/C++ 项目中使用第三方库有两种方式：
>
> 1. 第三方库在项目外部单独构建：从库的官网或是系统包管理程序上下载预编译好的包，或者事先在项目外部的其他路径下使用库的源码进行编译
> 2. 第三方库的构建集成到项目的构建过程里，从源码开始编译

详情可以看这个链接[cmake集成第三方库 - bw_0927 - 博客园 (cnblogs.com)](https://www.cnblogs.com/my_life/articles/14479973.html)

简单总结下，分4种情况导入第三方库

- 第三方库使用 CMake, 想集成到项目的构建过程里：使用`FetchContent`下载源码并构建
- 第三方库未使用 CMake，将其集成到项目的构建过程里：使用`add_custom_command`和`add_custom_target` 编译需要的库，以及创建target。
- 第三方库使用 CMake，在项目外部构建：使用`find_package`查找安装在本地的库。
- 第三方库未使用 CMake，在项目外部构建：`find_package`或者自定义`FindXXX.cmake`，其中包含`find_path`和`find_library`等命令帮助找到库。
  - 并不是第三方库没有使用CMake就不能使用find_package引入了，cmake官方有写很多常用库的find脚本。[cmake-modules(7) — CMake 3.24.2 Documentation](https://cmake.org/cmake/help/latest/manual/cmake-modules.7.html#find-modules)

-------

下面再说说我个人的经验：

第一种和第三种情况比较常见，第二种和第四种不太常见。

第一种方式使用`FetchContent`的本质也是使用`add_subdirectory`的方式引入项目，因为需要在配置过程中通过网络下载源代码，个人不太喜欢。我比较喜欢第三方库源代码已经存在项目中比如说`3rdparty`目录下，构建也集成到项目中的方式，这样可以使构建过程不依赖于网络。

我的替代方案是下载源代码到项目中，然后使用`add_subdirectory`命令配置第三方库，生成target。

对于生成较少target的库，只需要`add_subdirectory`以及用`option`或`set`控制需要的target即可，可以看看这个[项目](https://github.com/BoomingTech/Piccolo/blob/main/engine/3rdparty/CMakeLists.txt)里的第三方库的组织。

可生成的target的一般用bool变量控制是否生成，具体的变量有哪些可以看build目录下`CMakeCache.txt`这个文件，或者看看第三方库的CMakeLists.txt，通过搜索关键字(target、library、库名)快速定位，然后就可以用`option`设置变量值

```cmake
# 以spdlog为例，通过option命令我们控制了变量的值为off
# 这样就不会生成相应的target
if(NOT TARGET spdlog)
    option(SPDLOG_BUILD_EXAMPLE "" OFF)
    option(SPDLOG_INSTALL "" OFF)
    add_subdirectory(spdlog)
    set_target_properties(spdlog PROPERTIES FOLDER ${third_party_folder}/spdlog)
endif()
```



----

一些Header-only的库例如Eigen，引入这些库不需要编译。Eigen库的顶级目录下的CMakeLists.txt会生成一堆我们不需要的target，我们需要的实际是一个名为eigen的target，其余的target主要是用于测试等用途。

解决方法有二：

- 【不推荐】① 使用`add_subdirectory`引入Eigen时设置第三个参数`EXCLUDE_FROM_ALL`跳过子目录中的target，用option控制生成的target也可达到一样的效果，但这种方法还是编译了这种头文件库，会导致configure阶段多花时间，我们没有必要用这种方式。

```cmake
if(NOT TARGET eigen)
    add_subdirectory(eigen-3.4.0 eigen-3.4.0/build EXCLUDE_FROM_ALL) #产生的target名字由第三方库中CMakeLists.txt决定
endif()
```

- 【推荐】② 对于头文件库，我们只需要include即可，拿到源代码路径后自己创建一个简单的 target。因为eigen库头文件分散在很多文件夹不好指明，所以以stb为例。

```cmake
file(GLOB stb_sources CONFIGURE_DEPENDS  "${CMAKE_CURRENT_SOURCE_DIR}/stb/*.h")

#INTERFACE关键字必须加
#stb_sources可以不加，但这样在生成的项目中看不到stb这个target以及其中的文件了
add_library(stb INTERFACE ${stb_sources})

#必须将头文件库的源代码目录include进我们创建的target中
target_include_directories(stb INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/stb)

#设置目录，在IDE中打开会比较清晰
set_target_properties(stb PROPERTIES FOLDER ${third_party_folder}/stb)
```



----

对于比较大的库，构建比较复杂，同时也没有定制代码的需求，我们还是让用户在自己的主机上预安装这些库，通过`find_package`的方式导入。例如：qt、opencv。



# 使用第三方库

`add_subdirectory`后，第三方库的 target 都会进入到当前项目的作用域里，使用`target_link_libraries` 即可将需要的库链接到我们自己项目的target中

`find_package`也是找到第三方库的某个target，如果不清楚有哪些target，可以

1. 看源代码的CMakeLists.txt等于cmake有关的文件【第三方库用了cmake】
2. 看官方文档：[cmake-modules(7) — CMake 3.24.2 Documentation](https://cmake.org/cmake/help/latest/manual/cmake-modules.7.html#find-modules) 【第三方库没用cmake】
3. 看本地cmake目录下的这个路径`path_to_your_cmake/share/cmake-<version>/Modules`中的cmake文件【第三方库没用cmake】



无论是何种方式，我们都是面向作用域为整个项目的target，都是使用`target_link_libraries`链接库

对于头文件库，如果没有通过`add_library`等命令设成target的话，可以直接使用`target_include_directories`将头文件库目录包含进来我们的target中



# 补充

## add_subdirectory

官方文档：[add_subdirectory — CMake 3.24.2 Documentation](https://cmake.org/cmake/help/latest/command/add_subdirectory.html?highlight=add_subdirectory)

简单说就是指定一个目录，然后执行该目录下的CMakeLists.txt，第二个参数是输出文件的目录，第三个参数可以阻止子目录的target参与到构建当中

> add_subdirectory (source_dir [binary_dir] [EXCLUDE_FROM_ALL])
>
> 命令解析
>
> - **`source_dir`**
>   **必选参数**。该参数指定一个子目录，子目录下应该包含`CMakeLists.txt`文件和代码文件。子目录可以是相对路径也可以是绝对路径，如果是相对路径，则是相对当前目录的一个相对路径。
> - **`binary_dir`**
>   **可选参数**。该参数指定一个目录，用于存放输出文件。可以是相对路径也可以是绝对路径，如果是相对路径，则是相对当前输出目录的一个相对路径。如果该参数没有指定，则默认的输出目录使用`source_dir`。
> - **`EXCLUDE_FROM_ALL`**
>   **可选参数**。当指定了该参数，则子目录下的目标不会被父目录下的目标文件包含进去，父目录的`CMakeLists.txt`不会构建子目录的目标文件，必须在子目录下显式去构建。`例外情况：当父目录的目标依赖于子目录的目标，则子目录的目标仍然会被构建出来以满足依赖关系（例如使用了target_link_libraries）`。



## FetchContent

官方文档：[FetchContent — CMake 3.24.2 Documentation](https://cmake.org/cmake/help/latest/module/FetchContent.html)

FetchContent的使用主要是`FetchContent_Declare`以及`FetchContent_MakeAvailable`这两个命令

FetchContent_Populate在 Configure 阶段下载代码，所有使用 FetchContent 模块下载的源码相关目录都在 build 目录下的`_deps`文件夹里。

```cmake
include(FetchContent)  #声明需要使用cmake 3.11之后内置的FetchContent模块
FetchContent_Declare(  #声明第三方库的信息，包括名字、下载地址、版本号等
    libName
    GIT_REPOSITORY https://github.com/xxx/xxx.git
    GIT_TAG xxx
)
set(libName_BUILD_TESTS OFF) #设置变量用于关掉lib的测试
#以下内容等同于FetchContent_MakeAvailable(libName)命令，该命令在cmake 3.14之后引入
FetchContent_GetProperties(libName) #获取第三方库的信息，包括libName_POPULATED、SOURCE/BINARY_DIR等变量（需要已经执行过一次populate）
if(NOT libName_POPULATED)
    FetchContent_Populate(libName) #下载第三方库，并设置libNAME_POPULATED、libName_SOURCE_DIR、libName_BINARY_DIR等全局变量
    add_subdirectory(${libName_SOURCE_DIR} ${libName_BINARY_DIR}) #add_subdirectory会执行第三方库顶级目录下的CMakeLists.txt
endif()
```

注：cmake在configure阶段生成的全局变量可以在build目录下的CMakeCache.txt文件中找到

<span id='find_package'></span>

## find_package

官方文档：[find_package — CMake 3.24.2 Documentation](https://cmake.org/cmake/help/latest/command/find_package.html?highlight=find_package)

主要需要理解find_package是怎么搜索的，简而言之调用find_package(XXX)会寻找FindXXX.cmake

FindXXX.cmake文件中通过find_path、find_library等命令在指定目录下寻找头文件和动态库文件的位置

FindXXX.cmake文件的搜索路径为`CMAKE_MODULE_PATH`，而我们自己的FindXXX.cmake通常放在项目的cmake目录下，因此通常写如下代码添加项目中的cmake目录到`CMAKE_MODULE_PATH`变量

```cmake
set(CMAKE_MODULE_PATH "${PROJECT_SOURCE_DIR}/cmake" ${CMAKE_MODULE_PATH})
```

> 在Module模式中，cmake需要找到一个叫做`Find<LibraryName>.cmake`的文件。这个文件负责找到库所在的路径，为我们的项目引入头文件路径和库文件路径。cmake搜索这个文件的路径有两个，一个是上文提到的cmake安装目录下的`share/cmake-<version>/Modules`目录，另一个是我们指定的`CMAKE_MODULE_PATH`的所在目录。
>
> 如果Module模式搜索失败，没有找到对应的`Find<LibraryName>.cmake`文件，则转入Config模式进行搜索。它主要通过`<LibraryName>Config.cmake` or `<lower-case-package-name>-config.cmake`这两个文件来引入我们需要的库。以我们刚刚安装的glog库为例，在我们安装之后，它在`/usr/local/lib/cmake/glog/`目录下生成了`glog-config.cmake`文件，而`/usr/local/lib/cmake/<LibraryName>/`正是find_package函数的搜索路径之一。（find_package的搜索路径是一系列的集合，而且在linux，windows，mac上都会有所区别，需要的可以参考[官方文档](https://cmake.org/cmake/help/latest/command/find_package.html?highlight=find_package)）
>
> 由以上的例子可以看到，对于原生支持Cmake编译和安装的库通常会安装Config模式的配置文件到对应目录，这个配置文件直接配置了头文件库文件的路径以及各种cmake变量供find_package使用。而对于非由cmake编译的项目，我们通常会编写一个`Find<LibraryName>.cmake`，通过脚本来获取头文件、库文件等信息。通常，原生支持cmake的项目库安装时会拷贝一份XXXConfig.cmake到系统目录中，因此在没有显式指定搜索路径时也可以顺利找到。



## find_path/library

官方文档：[find_path — CMake 3.24.2 Documentation](https://cmake.org/cmake/help/latest/command/find_path.html?highlight=find_path)、[find_library — CMake 3.24.2 Documentation](https://cmake.org/cmake/help/latest/command/find_library.html?highlight=find_library)

会看就行，放个简单的例子：FindAdd.cmake

```cmake
# 在指定目录下寻找头文件和动态库文件的位置，可以指定多个目标路径
find_path(ADD_INCLUDE_DIR libadd.h /usr/include/ /usr/local/include ${CMAKE_SOURCE_DIR}/ModuleMode)
find_library(ADD_LIBRARY NAMES add PATHS /usr/lib/add /usr/local/lib/add ${CMAKE_SOURCE_DIR}/ModuleMode)

if (ADD_INCLUDE_DIR AND ADD_LIBRARY)
    set(ADD_FOUND TRUE)
endif (ADD_INCLUDE_DIR AND ADD_LIBRARY)
#以下为调用find_package，先找到FindAdd.cmake，然后找到add库
set(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake;${CMAKE_MODULE_PATH}")
find_package(ADD)
if(ADD_FOUND)
    target_include_directories(addtest PRIVATE ${ADD_INCLUDE_DIR})
    target_link_libraries(addtest ${ADD_LIBRARY})
else(ADD_FOUND)
    message(FATAL_ERROR "ADD library not found")
endif(ADD_FOUND)
```

因为第三方库都是通过`find_path`、`find_library`找到的，所以如果我们使用的第三方库是基于cmake构建的，且带有FindXXX.cmake文件，我们需要按照FindXXX.cmake中指定的path安装，或者需要设定相应的环境变量。下图为FindCUDA.cmake中的一段，我们需要在环境变量中设置变量`CUDA_PATH`或`CUDA_BIN_PATH`为CUDA_TOOKIT的根目录

![image-20220926212727571](https://gnoy-nus.github.io/img/in-post/cmake_structure/imgs/image-20220926212727571.png)

## add_custom_command/target

官方文档：[add_custom_command — CMake 3.24.2 Documentation](https://cmake.org/cmake/help/latest/command/add_custom_command.html?highlight=add_custom_command) 、[add_custom_target — CMake 3.24.2 Documentation](https://cmake.org/cmake/help/latest/command/add_custom_target.html?highlight=add_custom_target)

感觉用不太到，就只放个例子在这

```cmake
# 创建一个命令用于编译出uSockets的静态库，并且创建好头文件目录
add_custom_command(
    OUTPUT ${uWebSockets-git_SOURCE_DIR}/uSockets/uSockets.a
    COMMAND cp -r src uWebSockets && make
    WORKING_DIRECTORY ${uWebSockets-git_SOURCE_DIR}
    COMMENT "build uSockets"
    VERBATIM
)
# 创建一个自定义target，依赖上面自定义命令的OUTPUT，但这样CMake还不会编译这个target，还需要一个真正的target依赖此target
add_custom_target(uSockets DEPENDS ${uWebSockets-git_SOURCE_DIR}/uSockets/uSockets.a)

# 创建一个imported target，依赖上面的自定义target，从而确保在使用这个imported target时，上面的编译命令能被执行
add_library(uWebSockets STATIC IMPORTED)
set_property(TARGET uWebSockets PROPERTY IMPORTED_LOCATION ${uWebSockets-git_SOURCE_DIR}/uSockets/uSockets.a)
set_target_properties(
    uWebSockets PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES "${uWebSockets-git_SOURCE_DIR};${uWebSockets-git_SOURCE_DIR}/uSockets/src"
)
add_dependencies(uWebSockets uSockets) # 见上面add_custom_target的说明
```



# 参考

- [详解gitignore的使用方法，让你尽情使用git add . - 知乎 (zhihu.com)](https://zhuanlan.zhihu.com/p/264995020)
- [cmake集成第三方库 - bw_0927 - 博客园 (cnblogs.com)](https://www.cnblogs.com/my_life/articles/14479973.html)
- [Cmake命令之add_subdirectory介绍 - 简书 (jianshu.com)](https://www.jianshu.com/p/07acea4e86a3)
- [Piccolo/CMakeLists.txt at main · BoomingTech/Piccolo (github.com)](https://github.com/BoomingTech/Piccolo/blob/main/engine/3rdparty/CMakeLists.txt)
- [Cmake之深入理解find_package()的用法 - 知乎 (zhihu.com)](https://zhuanlan.zhihu.com/p/97369704)



# download

[modern-cmake.zip][1]  

[MyCmakeTemplate.zip][2]

[modern-cmake.pdf][3]

[1]: https://gnoy-nus.github.io/download/cmake/modern-cmake.zip

[2]:https://gnoy-nus.github.io/download/cmake/MyCmakeTemplate.zip

[3]:https://gnoy-nus.github.io/download/cmake/modern-cmake.pdf